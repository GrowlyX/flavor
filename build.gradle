plugins {
    id "maven-publish"
    id "com.jfrog.artifactory" version "4.28.2"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "org.jetbrains.kotlin.jvm" version "1.7.10"
}

group "gg.scala.flavor"
version "0.1.6"

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:1.7.10"
    compileOnly "org.jetbrains.kotlin:kotlin-reflect:1.7.10"
    compileOnly "org.reflections:reflections:0.10.2"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
}

compileKotlin {
    kotlinOptions.javaParameters = true
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    archiveClassifier.set("")
    archiveFileName.set("flavor.jar")

    exclude "**/*.kotlin_metadata"
    mergeServiceFiles()
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }

    repositories {
        maven {
            name "scala"
            url "${artifactory_contextUrl}/${artifactory_release}"
            credentials {
                username "${artifactory_user}"
                password "${artifactory_password}"
            }
        }
    }
}

artifactory {
    this.contextUrl = "${artifactory_contextUrl}"

    publish {
        repository {
            repoKey = "${artifactory_release_local}"
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
    }
}

tasks.build.dependsOn(
    shadowJar,
    publishShadowPublicationToScalaRepository
)
